buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" apply false
    id "io.gitlab.arturbosch.detekt" apply false
}

allprojects {
    group 'org.livingdoc'
    version '2.0-SNAPSHOT'

    ext.gradleScriptDir = "${rootProject.projectDir}/gradle"
}

subprojects { subproject ->
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    sourceCompatibility = 1.8

    clean {
        delete "$rootProject.projectDir/build"
    }
    
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives(javadocJar)
        archives(sourcesJar)
    }

    if(!subproject.name.endsWith("api") && !subproject.name.endsWith("documentation")){
        apply from: "${gradleScriptDir}/kotlin.gradle"
    }

    dependencies {
        testImplementation ("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
        testImplementation ("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")

        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    }

    test {
        testLogging { events "skipped", "failed" }
    }
}
